;
;Program to search on the provided linked list.
;Name to be input from the keyboard.
;Result to be displayed on the monitor
;
;Initialization
;
        .ORIG x3000
        AND     R1,R1,#0
        AND     R2,R2,#0
        AND     R3,R3,#0
        AND     R4,R4,#0
        AND     R5,R5,#0
        AND     R6,R6,#0
        LEA     R1,Name
        LEA     R0,Prompt
        TRAP    x22         ;output the prompt
;        
;Input the name
;
Input   GETC
        OUT                 ;echo the characters on the console
        ADD     R2,R0,#-10
        BRz     SEARCH      ;if the input is Enter ,jump to search phase
        STR     R0,R1,#0
        ADD     R1,R1,#1
        BRnzp   Input
;
;Search the information matching the input name
;
SEARCH  LD      R5,LinAd    ;R5 is the pointer that connects the list
INI     LEA     R1,Name     ;R1<-address of the input name
        LDR     R5,R5,#0    ;pointeri->pointeri+1
        BRz     Final       ;Final means the ending of search
        LDR     R2,R5,#2    ;R2<-address of the first name
Next1   LDR     R3,R1,#0    ;R3 contains the character of the input name
        LDR     R4,R2,#0    ;R4 contains the character of the name from the list
        BRz     CHECK1      ;check whether M[R1] equals M[R2]
FCMP    NOT     R3,R3
        ADD     R3,R3,#1
        ADD     R3,R3,R4
        BRnp    LCMP        ;compare the input name with the last name
        ADD     R1,R1,#1    
        ADD     R2,R2,#1    
        BRnzp   Next1       ;compare the charater one by one
        
CHECK1  ADD     R3,R3,#0
        BRz     Done        ;if M[R1] equals M[R2],jump to the Done

LCMP    LEA     R1,Name     
        LDR     R2,R5,#3    ;R2<-address of the last name
Next2   LDR     R3,R1,#0    
        LDR     R4,R2,#0    
        BRz     CHECK2      ;check whether M[R1] equals M[R2]
        NOT     R3,R3
        ADD     R3,R3,#1
        ADD     R3,R3,R4
        BRnp    Fail        ;fail to match with this name
        ADD     R1,R1,#1
        ADD     R2,R2,#1
        BRnzp   Next2        

CHECK2  ADD     R3,R3,#0
        BRz     Done        ;if M[R1] equals M[R2],jump to the Done
        BRnzp   INI      ;compare the input name with the next person

Done    LDR     R0,R5,#2    ;output the first name
        PUTS
        LD      R0,BLANK
        OUT
        LDR     R0,R5,#3    ;output the last name
        PUTS
        LD      R0,BLANK
        OUT
        LDR     R0,R5,#1    ;output the room member
        PUTS
        LD      R0,ENTER
        OUT
        ADD     R6,R6,#1    ;count the number of successful output
        BRnzp   INI
        
Fail    ADD     R6,R6,#0
        BRnzp   INI         ;compare the input name with the next person
        
Final   ADD     R6,R6,#0
        BRz     NotF
        BRnzp   END
        
NotF    LEA     R0,RESULT1
        PUTS

END     TRAP    x25
        

Name    .BLKW 16     
BLANK   .FILL        #32
ENTER   .FILL        #10
LinAd   .FILL        x4000   
Prompt  .STRINGZ     "Enter a name: "
RESULT1 .STRINGZ     "Not found"
        
        
        .END